{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "904d2c58",
   "metadata": {},
   "source": [
    "## MLP on DE with ODE Losses"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0f64b300",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Importing packages\n",
    "using Lux\n",
    "using Zygote\n",
    "using Optimization\n",
    "using OptimizationOptimisers\n",
    "using Random\n",
    "using Statistics\n",
    "using DifferentialEquations\n",
    "using Plots\n",
    "using ComponentArrays\n",
    "using NNlib"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be07ee4f",
   "metadata": {},
   "outputs": [],
   "source": [
    "struct SIR_params\n",
    "    β::Float32\n",
    "    γ::Float32\n",
    "end\n",
    "\n",
    "# SIR model method for DifferentialEquations call \n",
    "function SIR_model!(du, u, p::SIR_params, t)\n",
    "    S, I, R = u\n",
    "    du[1] = -p.β * S * I \n",
    "    du[2] =  p.β * S * I - p.γ * I\n",
    "    du[3] =  p.γ * I\n",
    "end\n",
    "\n",
    "# SIR model non mutating function method to calculate NN RHS\n",
    "SIR_model(y::AbstractMatrix{<:Real}, p::SIR_params) = begin\n",
    "\n",
    "    dS = - p.β .* y[1, :] .* y[2, :]        \n",
    "    dI = p.β .* y[1,:] .* y[2, :] - p.γ .* y[2, :]\n",
    "    dR = p.γ .* y[2, :]                         \n",
    "    permutedims(hcat(dS, dI, dR))\n",
    "end\n",
    "\n",
    "SIR_model(y::AbstractVector{<:Real}, p::SIR_params) = begin\n",
    "\n",
    "    dS = - p.β .* y[1] .* y[2]        \n",
    "    dI = p.β .* y[1] .* y[2] - p.γ .* y[2]\n",
    "    dR = p.γ .* y[2]                         \n",
    "    permutedims(hcat(dS, dI, dR))\n",
    "end\n",
    "\n",
    "\n",
    "# Fast default RNG. Does not get messed up with a global RNG\n",
    "rng = Random.MersenneTwister(42)\n",
    "\n",
    "true_SIR_par = SIR_params(0.30, 0.1)\n",
    "t_span = (0.0, 60.0)\n",
    "N_obs = 101\n",
    "\n",
    "# Collect seems to turn an iterable into an array\n",
    "t_obs_array = range(t_span[1], t_span[2], length=N_obs) |> collect\n",
    "u0 = [0.999, 0.001, 0.0]\n",
    "\n",
    "prob = DifferentialEquations.ODEProblem(SIR_model!, u0, t_span, true_SIR_par)\n",
    "sol = DifferentialEquations.solve(\n",
    "    prob,\n",
    "    DifferentialEquations.Tsit5();\n",
    "    saveat = t_obs_array\n",
    ")\n",
    "sol_ = Array(sol)\n",
    "\n",
    "# Add some noise\n",
    "noise_sd = 0.05\n",
    "noisy_data = clamp.(begin\n",
    "    sol_ + noise_sd.* Random.randn(rng, size(sol_)...) \n",
    "end,0, Inf)\n",
    "\n",
    "# Convert to Float32 to keep everyone happy\n",
    "t_train = Float32.(t_obs_array)\n",
    "Y_train = Float32.(noisy_data)\n",
    "y0_obs  = Y_train[:, 1]     # IC for penalty\n",
    "\n",
    "#= Plots.plot(sol)\n",
    "Plots.scatter!(t_train, Y_train[1,:])\n",
    "Plots.scatter!(t_train, Y_train[2,:])\n",
    "Plots.scatter!(t_train, Y_train[3,:]) =#\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c83afd86",
   "metadata": {},
   "outputs": [],
   "source": [
    "NN_model = Lux.Chain(\n",
    "    Lux.Dense(1, 10, tanh),\n",
    "    Lux.Dense(10, 10, tanh),\n",
    "    Lux.Dense(10, 3, NNlib.softplus) # forces outputs to be positive\n",
    ")\n",
    "\n",
    "ps, st = Lux.setup(rng, NN_model)\n",
    "ps = ComponentArrays.ComponentArray(ps)\n",
    "\n",
    "function predict_NN(model, ps, st, t_vec::AbstractVector{<:Real})\n",
    "    t_in = reshape(Float32.(t_vec), 1, :)\n",
    "    ŷ_n , _ = Lux.apply(model, t_in, ps, st)\n",
    "    return ŷ_n\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "26087799",
   "metadata": {},
   "outputs": [],
   "source": [
    "function MSE(ŷ, y)\n",
    "    Statistics.mean(abs2, vec(ŷ .- y))\n",
    "end\n",
    "\n",
    "λ_ic = 1.0f0*100\n",
    "λ_ode = 1.0f0*2\n",
    "\n",
    "function loss_supervised(ps, ctx)\n",
    "    #ctx is a named tuple holding everything non-trainable\n",
    "    model = ctx.model\n",
    "    st = ctx.st\n",
    "    t_train = ctx.t_train\n",
    "    Y_train = ctx.Y_train\n",
    "    y0_obs = ctx.y0_obs\n",
    "    λ_ic = ctx.λ_ic\n",
    "    λ_ode = ctx.λ_ode\n",
    "    SIR_par = ctx.SIR_par\n",
    "\n",
    "    smodel = Lux.StatefulLuxLayer(model, ps, st)\n",
    "    ŷ = smodel(t_train')\n",
    "    data_mse = MSE(ŷ, Y_train)\n",
    "    ic_mse = Statistics.mean(abs2, ŷ[:,1] .- y0_obs)\n",
    "    \n",
    "\n",
    "    f = tn -> begin\n",
    "        ŷ_NN = smodel([tn])\n",
    "        f_ŷ_NN = SIR_model(ŷ_NN, SIR_par)\n",
    "        dNNdt_tn = Zygote.jacobian(smodel, [tn])[1]\n",
    "        Statistics.mean(abs2, f_ŷ_NN .- dNNdt_tn)\n",
    "        end\n",
    "    ode_mse = mean(f.(ctx.t_dense))\n",
    "    \n",
    "    return data_mse + ic_mse *λ_ic + λ_ode*ode_mse  \n",
    "end\n",
    "\n",
    "t_dense = Float32.(range(t_train[begin], t_train[end], length=200) |> collect)\n",
    "ctx = (\n",
    "    model = NN_model,\n",
    "    st = st,\n",
    "    t_train = t_train,\n",
    "    Y_train = Y_train,\n",
    "    y0_obs = y0_obs,\n",
    "    λ_ic = λ_ic,\n",
    "    λ_ode = λ_ode,\n",
    "    t_dense = t_dense,\n",
    "    SIR_par = true_SIR_par\n",
    "\n",
    ")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fb52d1a4",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "optfun = Optimization.OptimizationFunction(\n",
    "    (x, p) -> loss_supervised(x,p),\n",
    "    Optimization.AutoZygote()\n",
    ")\n",
    "prob_fit = Optimization.OptimizationProblem(optfun, ps, ctx)\n",
    "res = Optimization.solve(prob_fit, OptimizationOptimisers.Adam(1e-03); maxiters=5_000)\n",
    "ps_trained = res.u"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4bd5976c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Build a stateful model bound to the trained params\n",
    "smodel = Lux.StatefulLuxLayer(NN_model, ps_trained, st)\n",
    "\n",
    "# A smooth time grid for plotting the NN curve\n",
    "t_plot = Float32.(collect(range(t_span[1], t_span[2], length=400)))\n",
    "\n",
    "# NN predictions on the smooth grid (3×N)\n",
    "ŷ_plot = smodel(permutedims(t_plot))  # 1×N input expected; returns 3×N\n",
    "\n",
    "# Split true, NN, and noisy series\n",
    "S_true, I_true, R_true = sol_[1, :], sol_[2, :], sol_[3, :]\n",
    "S_nn,   I_nn,   R_nn   = vec(ŷ_plot[1, :]), vec(ŷ_plot[2, :]), vec(ŷ_plot[3, :])\n",
    "S_noisy, I_noisy, R_noisy = Y_train[1, :], Y_train[2, :], Y_train[3, :]\n",
    "\n",
    "# Panel 1: S(t)\n",
    "p1 = Plots.plot(t_plot, S_nn; label = \"NN S(t)\", linewidth = 2)\n",
    "Plots.plot!(p1, t_obs_array, S_true; label = \"ODE S(t)\", linestyle = :dash, linewidth = 2)\n",
    "Plots.scatter!(p1, t_train, S_noisy; label = \"data S\", ms = 3, alpha = 0.7)\n",
    "Plots.xlabel!(p1, \"t\")\n",
    "Plots.ylabel!(p1, \"S(t)\")\n",
    "\n",
    "# Panel 2: I(t)\n",
    "p2 = Plots.plot(t_plot, I_nn; label = \"NN I(t)\", linewidth = 2)\n",
    "Plots.plot!(p2, t_obs_array, I_true; label = \"ODE I(t)\", linestyle = :dash, linewidth = 2)\n",
    "Plots.scatter!(p2, t_train, I_noisy; label = \"data I\", ms = 3, alpha = 0.7)\n",
    "Plots.xlabel!(p2, \"t\")\n",
    "Plots.ylabel!(p2, \"I(t)\")\n",
    "\n",
    "# Panel 3: R(t)\n",
    "p3 = Plots.plot(t_plot, R_nn; label = \"NN R(t)\", linewidth = 2)\n",
    "Plots.plot!(p3, t_obs_array, R_true; label = \"ODE R(t)\", linestyle = :dash, linewidth = 2)\n",
    "Plots.scatter!(p3, t_train, R_noisy; label = \"data R\", ms = 3, alpha = 0.7)\n",
    "Plots.xlabel!(p3, \"t\")\n",
    "Plots.ylabel!(p3, \"R(t)\")\n",
    "\n",
    "# Combine panels\n",
    "plt = Plots.plot(p1, p2, p3; layout = (3, 1), size = (600, 900))\n",
    "display(plt)\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia (Learning-Julia) 1.11.7",
   "language": "julia",
   "name": "julia-_learning-julia_-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
